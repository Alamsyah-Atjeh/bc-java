

jar.archiveBaseName = "bcutil-$vmrange"


sourceSets {
    java9 {
        java {
            srcDirs = ['src/main/jdk1.9']
        }
    }

    test11 {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += test.output
            srcDir(files("src/test/java"))
        }
    }
}


dependencies {
    implementation project(':prov')
    implementation files("${bc_prov}")
    implementation project(path: ':core')
    implementation project(path: ':prov')

    testImplementation group: 'junit', name: 'junit', version: '4.11'

    java9Implementation files([sourceSets.main.output.classesDirs]) {
        builtBy compileJava
    }
    java9Implementation project(':prov')
    java9Implementation project(':core')
    java9Implementation project(path:':prov')

    test11Implementation group: 'junit', name: 'junit', version: '4.13.2'
    test11Implementation files("$bc_prov")
    test11Implementation project(path: ':core')
}

compileJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    targetCompatibility = 1.8;
    sourceCompatibility = 1.8;
}


compileJava9Java {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }

    options.compilerArgs += [
            '--module-path', "${bc_prov}"
    ]

    sourceCompatibility = 9
    targetCompatibility = 9
    options.sourcepath = files(['src/main/java', 'src/main/jdk1.9'])
}


jar {
    from sourceSets.main.output
    into('META-INF/versions/9') {
        from sourceSets.java9.output
    }

    //
    // This exists because the above 'sourceSets.java9.output' is randomly
    // empty even though the classes have been built and are where
    // they should be.
    into('META-INF/versions/9') {
        from "${projectDir}/build/classes/java/java9"
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }

    manifest.attributes('Multi-Release': 'true')
    manifest.attributes('Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8')
    manifest.attributes('Export-Package': 'org.bouncycastle.*')
    manifest.attributes('Import-Package': 'java.*;resolution:=optional;javax.*;resolution:=optional')
}



task sourcesJar(type: Jar) {
    archiveBaseName = jar.archiveBaseName
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    exclude("**/*.so")
    into('META-INF/versions/9') {
        from sourceSets.java9.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveBaseName = jar.archiveBaseName
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    forkEvery = 1;
    maxParallelForks = 8;
}


task test11(type: Test) {

    dependsOn(jar)

    testClassesDirs = sourceSets.test11.output.classesDirs
    classpath = sourceSets.test11.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = true

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(11)
    }

    jvmArgs = ['-Dtest.java.version.prefix=11.']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        includeTestsMatching "All11Test*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

task test17(type: Test) {

    dependsOn(jar)

    testClassesDirs = sourceSets.test11.output.classesDirs
    classpath = sourceSets.test11.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = true

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }

    jvmArgs = ['-Dtest.java.version.prefix=17.']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        includeTestsMatching "All11Test*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}

task test21(type: Test) {

    dependsOn(jar)

    testClassesDirs = sourceSets.test11.output.classesDirs
    classpath = sourceSets.test11.runtimeClasspath + files(jar.archiveFile)

    forkEvery = 1;
    maxParallelForks = 8;

    systemProperty 'bc.test.data.home', bcTestDataHome
    maxHeapSize = "1536m"
    testLogging.showStandardStreams = true

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }

    jvmArgs = ['-Dtest.java.version.prefix=21']


    finalizedBy jacocoTestReport

    filter {
        includeTestsMatching "AllTest*"
        includeTestsMatching "All11Test*"
        if (project.hasProperty('excludeTests')) {
            excludeTestsMatching "${excludeTests}"
        }
    }
}